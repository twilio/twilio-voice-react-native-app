// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33
        kotlinVersion = "1.8.0"
        // use the same NDK version as the Voice SDK
        ndkVersion = "25.2.9519653"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.4.2")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:5.0.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.google.gms:google-services:4.4.0")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven {
            // Detox is installed from npm
            url("$rootDir/../node_modules/detox/Detox-android")
        }
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    afterEvaluate { subproject ->
        // fix detox with auth0 error, reference: https://github.com/auth0/react-native-auth0/issues/387#issuecomment-918483390
        if (subproject.name == 'react-native-auth0') {
            String taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
            if (taskRequests.contains("assembleAndroidTest")) {
                android {
                    defaultConfig {
                        manifestPlaceholders = [auth0Domain: "E2E_TEST", auth0Scheme: "com.E2E.TEST"]
                    }
                }
            }
        }
    }
}
